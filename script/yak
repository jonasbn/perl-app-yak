#!/usr/bin/env perl

use strict;
use warnings;
use v5.10; # say
           # stacked file tests, REF: https://perldoc.perl.org/functions/-X

use FindBin;
use lib "$FindBin::Bin/../lib";
use App::Yak;
use Getopt::Long; # GetOptions

binmode STDOUT, ":utf8";
binmode STDERR, ":utf8";

use constant FALSE   => 0;
use constant TRUE    => 1;
use constant SUCCESS => 0;

my $noconfig_flag    = FALSE;
my $nochecksums_flag = FALSE;
my $checksums_src    = '';
my $config_src       = '';
my $rv               = SUCCESS;

my $yak = App::Yak->new();
my $flags = {};

GetOptions ('debug'       => sub { $yak->debug(TRUE); $flags->{debug} = TRUE; },
            'nodebug'     => sub { $yak->nodebug(TRUE); $flags->{nodebug} = TRUE; },
            'verbose'     => sub { $yak->verbose(TRUE); $flags->{verbose} = TRUE; },
            'silent'      => sub { $yak->silent(TRUE); $flags->{silent} = TRUE; },
            'nocolor'     => sub { $yak->nocolor(TRUE); $flags->{nocolor} = TRUE; },
            'color'       => sub { $yak->color(TRUE); $flags->{color} = TRUE; },
            'noemoji'     => sub { $yak->noemoji(TRUE); $flags->{noemoji} = TRUE; },
            'emoji'       => sub { $yak->emoji(TRUE); $flags->{emoji} = TRUE; },
            'nochecksums' => sub { $flags->{nochecksums} = TRUE; },
            'noconfig'    => sub { $flags->{noconfig} = TRUE; },
            'checksums=s' => \&checksums_src,
            'config=s'    => \&config_src,
            'about'       => sub { $flags->{about} = TRUE; },
            'help'        => sub { $flags->{help} = TRUE; },
) or die "Error in command line arguments\n";

# Reading the config
my $config;

if (not $flags->{noconfig}) {
    $config = $yak->read_config();
}

# Reading the checksum data
$yak->read_checksums();

if ($flags->{help}) {
    exit $yak->print_help();
}

if ($flags->{about}) {
    exit $yak->print_about($flags, $config);
}

exit $yak->process(); 

sub checksums_src {
    my ($opt_name, $opt_value) = @_;
    $yak->checksums_src($opt_value);
    $flags->{checksums_src} = $opt_value;
}

sub config_src {
    my ($opt_name, $opt_value) = @_;
    $yak->config_src($opt_value);
    $flags->{config_src} = $opt_value;
}

__END__

=pod

=encoding UTF-8

=head1 yak

B<yak> - application to help with yak shaving for Git repositories etc.

=head1 SYNOPSIS

    $ yak
    üëçüèª./CODE_OF_CONDUCT.md
    üëçüèª./README.md
    üëçüèª./CONTRIBUTING.md

=head1 DESCRIPTION

The B<yak> I<shaver> can scan a directory for files, which can be classified as yaks in need of shaving. Meaning files which are maintained else where are often copy-pasted.

The file names can be configured in a central configuration file, like this:

F<$HOME/.config/yak/checksums.json>

    {
        "CONTRIBUTING.md": "15701b6b27e1d49ca6636f2695cfc49b6622c7152f74b14becc53850811db54f"
    }

If a file is encountered, which matches the name, the checksum of the encountered file is calculated and is compared to the checksum listed in the central file.

=over

=item * If they match, everything is okay

=item * If they differ, the difference has to be addressed

=back

The recommendation is to have the checksum in the central file, reflect the authoritative revision and hence you can overwrite the file in the directory you where inspecting.

Alternatively to specifying a checksum, you can specify a file URL:

    {
        "MANIFEST.SKIP": "file://MANIFEST.SKIP"
    }

The file pointed to has to be available in: F<$HOME/.config/yak/files>

Then C<yak> can calculate the checksum dynamically, based on the reference file and can based on invocation copy the reference file to the location of the evaluated file in the case where the two differ.

=head2 CHECKSUM DATA FILE EXAMPLE

This JSON file should be created as C<$HOME/.config/yak/checksums.json>.

    {
        "CODE_OF_CONDUCT.md": "da9eed24b35eed80ce28e07b02725dbb356cfa56500a1552a1410ab5c73af82c",
        "CONTRIBUTING.md": "file://CONTRIBUTING.md",
        "PULL_REQUEST_TEMPLATE.md": "91dabee84afd46f93894d1a266a773f3d46c2c0b1ae4813f0c7dba34df1dc260",
        "MANIFEST.SKIP": "file://MANIFEST.SKIP"
    }

=head2 IGNORING CERTAIN DIRECTORIES, FILES AND FILENAME PATTERNS

C<yak> supports the ability to ignore:

=over

=item * Files

=item * Directories

=item * Filename patterns

=back

This is accomplished using an implementation based on C<.gitignore>. To not intervene and to let C<git> and C<yak> work in harmony. The files used by C<yak> are named C<.yakignore>.

The mean that you can:

=over

=item * Specify patterns of files and directories in your configuration file, see L</CONFIGURATION>. This configuration will be overwritten if the next options are used.

=item * You can add an C<.yakignore> in the root of your repository and it will work for all files and directories in the file structure beneath it. Do note that the presence of this files, ignores and configuration in regard to using this feature. Meaning that disabling C<yak> ignores for a single repository can be accomplished by placing an empty C<.yakignore> file in the root of the repository.

=item * a I<child> C<.yakignore> can be placed in a subsequent directory, working on all files and directories beneath it, do note that directories specified to be ignored in the I<parent> C<.yakignore> are ignored and are not parsed and used.

=back

=head2 YAK IGNORE FILE EXAMPLE

    .git
    local

The above example specified the C<local> directory created by Perl's Carton. Another good candidate could be the C<.git> folder.

Since C<yak> is processing a directory structure recursively, specifying directories should speed up the processing. Specifying single files by name can be used to skip a file specified in the data source file temporarily.

=head1 INVOCATION

C<yak> takes the following command line arguments:

=over

=item * C<--verbose>, enables more verbose output, can be configured see L</CONFIGURATION>

=item * C<--silent>, disables output and you have to rely on the return value see L</RETURN VALUES> below.

=item * C<--debug>, enables debug output. can be configured see L</CONFIGURATION>

=item * C<--nodebug>, disables debug output even if confgured or provided as C<--debug>, see above

=item * C<--config [file]>, reads alternative configuration file instead of default, see L</CONFIGURATION>

=item * C<--noconfig>, disables reading of the configuration file, (see L</CONFIGURATION>) and you have to rely on the command line arguments

=item * C<--nochecksums>, disables reading of the global checksums file, see L</DATA SOURCE>

=item * C<--checksums [file]>, reads alternative checksums file instead of default, see L</DATA SOURCE>

=item * C<--color>, enables colorized output, enabled by default or can be configured, see L</CONFIGURATION>

=item * C<--nocolor>, disables colorized output, even if confgured or provided as C<--color>, see above

=item * C<--emoji>, enables emojis in output, enabled by default or can be configured, see L</CONFIGURATION>

=item * C<--noemoji>, disables emojis in output, even if confgured or provided as C<--emoji>, see above

=item * C<--about>, emits output on configuration and invocation and terminates with success

=item * C<--help>, emits help message listing all available options

=back

Command line arguments override the configuration.

=head2 RETURN VALUES

=over

=item * C<0>, success, everything is okay

=item * C<1>, failure, a located filed did not match the designated checksum

=back

Note that C<--about> return as success with out processing any data apart from reading configuration and parsing command line arguments.

=head1 ENVIRONMENT

C<yak> supports the following environment variables:

=over

=item * C<$CLICOLOR}>, if set to false (C<0>) it attempts to disable colorized output, if set to true (C<1>), it attempts to enable colorized output

=item * C<$CLICOLOR_FORCE>, if set to true (C<1>) it enables colorized output, if set to false (C<1>), it disables colorized output

=back

The order of precedence is as follows:

=over

=item 1. Environment (this section), C<$CLICOLOR_FORCE>

=item 2. Command line arguments, C<--nocolor> and C<--color> in that order, see L</INVOCATION>

=item 3. Environment (this section), C<$CLICOLOR>

=item 4. Configuration, see L</CONFIGURATION>, C<color> configuration option

=back

This aims to follow the proposed standard described in L<this article|https://bixense.com/clicolors/>.

=head1 CONFIGURATION

F<$HOME/.config/yak/config.yml>

C<yak> can be configured using the following paramters:

=over

=item * C<verbose>, enabling (C<true>) or disabling (C<false>) more verbose output

=item * C<debug>, enabling (C<true>) or disabling (C<false>) debug output

=item * C<color>, enabling (C<true>) or disabling (C<false>) colorized output

=item * C<emoji>, enabling (C<true>) or disabling (C<false>) colorized output

=item * C<success_emoji>

=item * C<failure_emoji>

=item * C<skip_emoji>

=item * C<yakignores>, specify a list of file directory names and patterns to be ignored

=back

Configuration can be overridden by command line arguments, see L</INVOCATION>.

=head2 EXAMPLE CONFIGURATION

This YAML file should be created as C<$HOME/.config/yak/config.yml>.

    verbose: false
    debug: false
    skip_emoji: ‚úñÔ∏è
    failure_emoji: ‚ùå
    success_emoji: ‚úÖ
    yakignores:
    - .git
    - local

=head1 DATA SOURCE

There are 3 ways to provide checksum data to C<yak>.

=over

=item * The default using: C<$HOME/.config/yak/checksums.json>, which can then be edited to match your needs

=item * Using a project or repository specific: C<.yaksums.json> located in the root of your project or repository directory

=item * Using an JSON file adhering to formatting described in this chapter, which can be located elsewhere on your file system and then pointed out using the command line argument: C<--checksums [file]>

=back

The default data source is described in the L</DESCRIPTION>. As a an alternative a per project file can be specified in the designated repository/directory.

The file should be named: C<.yaksums.json>

The contents follow the same format as the C<$HOME/.config/yak/checksums.json>.

This JSON file should look as follows:

    {
        "<filename>": "<sha256 checksum for the file specifed by the filename>"
    }

An example:

    {
        "CODE_OF_CONDUCT.md": "da9eed24b35eed80ce28e07b02725dbb356cfa56500a1552a1410ab5c73af82c",
        "CONTRIBUTING.md": "file://CONTRIBUTING.md",
        "PULL_REQUEST_TEMPLATE.md": "91dabee84afd46f93894d1a266a773f3d46c2c0b1ae4813f0c7dba34df1dc260",
        "MANIFEST.SKIP": "file://MANIFEST.SKIP"
    }

If you want to have B<Yak> help you checking for the presence of a file, specify the I<boolean> C<true> instead of a checksum.

    {
        "ISSUE_TEMPLATE.md": true,
        "README.md": true
    }

Or you can issue an error if a file is present, which should not be there, again using a I<boolean>, but set to C<false>.

    {
        ".vstags": false
    }

=head1 ISSUE REPORTING

If you experience any issues with B<yak> report these via GitHub. Please read L<the issue reporting template|https://github.com/jonasbn/perl-app-yak/blob/master/.github/ISSUE_TEMPLATE.md>.

=head1 AUTHOR

=over

=item * jonasbn, L<website|https://jonasbn.github.io/>

=back

=head1 COPYRIGHT

C<yak> and L<App::Yak>is (C) by Jonas Br√∏ms√∏, (jonasbn) 2018-2020

L<Image|https://unsplash.com/photos/3b3O75X0Jzg> used on the B<yak> L<website|https://jonasbn.github.io/perl-app-yak/> is under copyright by L<Shane Aldendorff|https://unsplash.com/@pluyar>.

=head1 LICENSE

C<yak> is released under the MIT License

=cut
